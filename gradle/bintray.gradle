import com.google.common.base.CaseFormat

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

// publishing
publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault('bintrayUser', 'fake_user')
    key = getPropertyOrUseDefault('bintrayKey', 'fake_key')
    publications = ['groovyMaven']
    publish = true

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        websiteUrl = 'https://github.com/agorapulse/micronaut-libraries'
        issueTrackerUrl = 'https://github.com/agorapulse/micronaut-libraries/issues'
        vcsUrl = 'https://github.com/agorapulse/micronaut-libraries.git'

        repo = 'libs' // or your repo name
        userOrg = 'agorapulse'
        name = projectName       // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    // dryRun = true // whether to run this as dry-run, without deploying
}


String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    if (hasProperty(propertyName)) {
        return getProperty(propertyName)
    }

    String envVarName = CaseFormat.LOWER_CAMEL.to(CaseFormat.UPPER_UNDERSCORE, propertyName)
    String envValue = System.getenv(envVarName)

    if (envValue) {
        return envValue
    }

    return defaultValue
}
